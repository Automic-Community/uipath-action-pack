*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        start_job
Test Template     Start Job Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    JobNumber         JobName         Force       Kill        Reason
Start job with valid Job Number having all valid data
                      [Tags]    main_scenarios
                      OK        ${Job_Number}     ${EMPTY}        ${Force}    ${Kill}     ${Reason}

ReUpload Package job with valid Job Name having duplicate jobs with same name
                      NOK       ${EMPTY}          ${Job_Name}     ${EMPTY}    ${EMPTY}    ${Reason}

Start job with Invalid or non existing JobNumber
                      NOK       Invalid           ${EMPTY}        ${EMPTY}    ${EMPTY}    ${EMPTY}

Start job with Invalid or non existing JobName
                      NOK       ${EMPTY}          Invalid         ${EMPTY}    ${EMPTY}    ${EMPTY}

Empty JobNumber and JobName and valid values in non mandatory fields
                      NOK       ${EMPTY}          ${EMPTY}        ${Force}    ${Kill}     ${Reason}

Start job with valid Job Name and Job Number
                      NOK       ${Job_Number1}    ${Job_Name1}    ${EMPTY}    ${EMPTY}    ${EMPTY}

Start job with valid Job Number where reason is not mandatory
                      OK        ${Job_Number2}    ${EMPTY}        ${EMPTY}    ${EMPTY}    ${EMPTY}

*** Keywords ***
Startup
    Log    *******Start Test "Start JOB"*******
    Log    *******Connect to AE*******
    Connect AE

Start Job Template
    [Arguments]    ${OK_NOK}    ${Job_Number}    ${Job_Name}    ${Force}    ${Kill}    ${Reason}
    [Documentation]    This test Starts jobs in UI Path.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK or NOK.
    Generate String
    Init Action    ${_Start_JOB_ACTION}
    Connect To UiPath
    Run Keyword If    '${Job_Name}' == 'Invalid'    Action Set    &UC4RB_CA7_JOBNAME#    ${Generated_Name}_${Time}
    ...    ELSE    Action Set    &UC4RB_CA7_JOBNAME#    ${Job_Name}
    Run Keyword If    '${Job_Number}' == 'Invalid'    Action Set    &UC4RB_CA7_JOBNO#    ${Generated_Name}_${Time}
    ...    ELSE    Action Set    &UC4RB_CA7_JOBNO#    ${Job_Number}
    Run Keyword If    '${Force}'    Action Set    &UC4RB_CA7_FORCE#    ${Force}
    Run Keyword If    '${Kill}'    Action Set    &UC4RB_CA7_KILL#    ${Kill}
    Run Keyword If    '${Reason}'    Action Set    &UC4RB_CA7_REASON#    ${Reason}
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success
    ...    ELSE    Assert Failure

Teardown
    Log    *******End Test "Start JOB"*******
