*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        create_new_release
Test Template     Create New Release Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    Name              ProcessName      ProcessVersion       EnvID
Create new Release with valid input values and non-existing ProcessKey for the given Environment
                      [Tags]    main_scenarios
                      OK        ${Name}           ${ProcessKey}    ${ProcessVersion}    ${Env_ID}

Create new Release with Invalid Name
                      NOK       Invalid           ${ProcessKey}    ${ProcessVersion}    ${Env_ID}

Create new Release with Invalid ProcessKey
                      NOK       ${Name}           Invalid          ${ProcessVersion}    ${Env_ID}

Create new Release with Invalid ProcessVersion
                      NOK       ${Name}           ${ProcessKey}    Invalid              ${Env_ID}

Create new Release with Invalid Environment Name
                      NOK       ${Name}           ${ProcessKey}    ${ProcessVersion}    Invalid

*** Keywords ***
Startup
    Log    *******Start Test "Create New Release"*******
    Log    *******Connect to AE*******
    Connect AE

Create New Release Template
    [Arguments]    ${OK_NOK}    ${Name}    ${ProcessName}    ${ProcessVersion}    ${EnvID}
    [Documentation]    This test get releases from UiPath.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK or NOK.
    ...    -${Name}: This value provide a name the release to be created.
    ...    -${ProcessName}: This value provide the unique identifier of the process to be associated with the environment.
    ...    -${ProcessVersion}: This value provide the version of the process.
    ...    -${EnvID}: This value provide the name of the environment to be associated with the process.
    Generate String
    Init Action    ${_CREATE_NEW_RELEASE_ACTION}
    Connect To UiPath
    Run Keyword If    '${Name}' == 'Invalid'    Action Set    &UC4RB_UIP_RELEASE_NAME#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_UIP_RELEASE_NAME#    ${Name}
    Run Keyword If    '${ProcessName}' == 'Invalid'    Action Set    &UC4RB_UIP_PROC_NAME#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_UIP_PROC_NAME#    ${ProcessName}
    Run Keyword If    '${ProcessVersion}' == 'Invalid'    Action Set    &UC4RB_UIP_PROC_VRSN#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_UIP_PROC_VRSN#    ${ProcessVersion}
    Run Keyword If    '${EnvID}' == 'Invalid'    Action Set    &UC4RB_UIP_ENV_ID#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_UIP_ENV_ID#    ${EnvID}
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE    Assert Failure
    Run Keyword If    '${OK_NOK}' == 'OK'    Action Object Variable Should Be Found    &UC4RB_UIP_RELEASE_ID#    ELSE    Action Object Variable Should Not Be Found    &UC4RB_UIP_RELEASE_ID#
    Run Keyword If    '${OK_NOK}' == 'OK'    Action Object Variable Should Be Found    &UC4RB_UIP_RELEASE_KEY#    ELSE    Action Object Variable Should Not Be Found    &UC4RB_UIP_RELEASE_KEY#

Teardown
    Log    *******End Test "Create New Release"*******
