*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        get_releases
Test Template     Get Releases Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    ProcessKey        EnvironmentName    Properties       Filter      OrderBy       Top         Skip       IncludeCount       Expand
Get Releases with valid Filter values
                      [Tags]    main_scenarios
                      OK        ${ProcessKey}     YES                ${EMPTY}         ${EMPTY}    ${OrderBy}    ${Top}      ${Skip}    ${IncludeCount}    ${Expand}

Get Releases with Empty Filter values
                      OK        ${EMPTY}          NO                 ${Empty}         ${Empty}    ${OrderBy}    ${Empty}    ${Skip}    ${IncludeCount}    ${Empty}

Get Releases with non-zero Skip value without Order By
                      NOK       ${EMPTY}          YES                ${Properties}    ${Empty}    ${Empty}    ${Empty}    ${Skip}    ${IncludeCount}    ${Empty}

Get Releases with Invalid or non existing Properties values
                      NOK       ${EMPTY}          YES                Invalid          ${Empty}    ${OrderBy}    ${Top}      ${Skip}    ${IncludeCount}    ${Expand}

Get Releases with Invalid or non existing Filter values
                      NOK       ${EMPTY}          YES                ${Properties}    Invalid     ${OrderBy}    ${Top}      ${Skip}    ${IncludeCount}    ${Expand}

*** Keywords ***
Startup
    Log    *******Start Test "Get Releases"*******
    Log    *******Connect to AE*******
    Connect AE

Get Releases Template
    [Arguments]    ${OK_NOK}    ${ProcessKey}    ${EnvironmentName}    ${Properties}    ${Filter}    ${OrderBy}
    ...    ${Top}    ${Skip}    ${IncludeCount}    ${Expand}
    [Documentation]    This test get releases from UiPath.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK or NOK.
    ...    -${ProcessKey}: This field specifies Process Name you want filter.
    ...    -${EnvironmentName}: This field specifies name of the environment.
    ...    -${Properties}: This value provide list of properties as comma separated to include in the response.
    ...    -${Filter}: This value provide filters to be applied for the results, based on a Boolean condition.
    ...    -${OrderBy}: This value specify the property to be used in ordering.
    ...    -${Top}: This value specify if the result should include only the first N items.
    ...    -${Skip}: This value specify if the result should skip the first N items.
    ...    -${IncludeCount}: This value specify whether if count of the matching results should be included in the response.
    ...    -${Expand}: This value provide related entities to be inlined.
    Generate String
    Init Action    ${_GET_RELEASES_ACTION}
    Connect To UiPath
    Run Keyword If    '${ProcessKey}' == 'Invalid'    Action Set    &UC4RB_UIP_PROC_NAME#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_UIP_PROC_NAME#    ${ProcessKey}
    Action Set    &UC4RB_UIP_LATEST_VERSION#    ${EnvironmentName}
    Run Keyword If    '${Properties}' == 'Invalid'    Action Set    &UC4RB_UIP_PROP#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_UIP_PROP#    ${Properties}
    Run Keyword If    '${Filter}' == 'Invalid'    Action Set    &UC4RB_UIP_FILTER#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_UIP_FILTER#    ${Filter}
    Run Keyword If    '${OrderBy}' == 'Invalid'    Action Set    &UC4RB_UIP_ORD_BY#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_UIP_ORD_BY#    ${OrderBy}
    Run Keyword If    '${Top}' == 'Invalid'    Action Set    &UC4RB_UIP_TOP#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_UIP_TOP#    ${Top}
    Run Keyword If    '${Skip}' == 'Invalid'    Action Set    &UC4RB_UIP_SKIP#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_UIP_SKIP#    ${Skip}
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE    Assert Failure
    Run Keyword If    '${OK_NOK}' == 'OK'    Action Object Variable Count Value Should Be    &UC4RB_UIPATH_RELEASES_COUNT#
    Run Keyword If    '${OK_NOK}' == 'OK' and '&UC4RB_UIPATH_RELEASES_COUNT#' > '0'    Action Object Variable Should Be Found    &UC4RB_UIPATH_RELEASES_KEYS#
    Run Keyword If    '${OK_NOK}' == 'OK' and '&UC4RB_UIPATH_RELEASES_COUNT#' > '0'    Action Object Variable Should Be Found    &UC4RB_UIPATH_RELEASES_IDS#
    Run Keyword If    '${OK_NOK}' == 'NOK'    Action Object Variable Should Not Be Found    &UC4RB_UIPATH_RELEASES_KEYS#
    Run Keyword If    '${OK_NOK}' == 'NOK'    Action Object Variable Should Not Be Found    &UC4RB_UIPATH_RELEASES_IDS#

Teardown
    Log    *******End Test "Get Releases"*******
